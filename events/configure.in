dnl $ZEL: configure.in,v 1.2 2011/08/25 23:47:10 wuestner Exp $
dnl Process this file with autoconf to produce a configure script.
dnl $ems/events
AC_INIT(ev_file.c)
AC_CONFIG_AUX_DIR(../confsupp)

lems=/usr/local/ems
AC_PREFIX_DEFAULT(/usr/local/ems)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_CANONICAL_SYSTEM

. $srcdir/patchlevel

AC_ARG_ENABLE(debug, [  --enable-debug ])
dnl Set compilation flags

AC_ARG_ENABLE(dynamic, [  --enable-dynamic ])
AC_MSG_CHECKING(whether we should use shared objects)

dynamic=false
if test "$enable_dynamic" = yes; then
  case $target_os in
    x_linux*)
      SHLIBSUFFIX=.so
      dynamic=true
      ;;
    osf*)
      SHLIBSUFFIX=.so
      dynamic=true
      ;;
    *)
      AC_MSG_WARN([dynamic linking? don't know how ...])
      ;;
  esac
else
  case $target_os in
    linux*)
      LDFLAGS="$LDFLAGS -static"
      ;;
    osf*)
      LDFLAGS="$LDFLAGS -non_shared -v"
      ;;
    ultrix*)
      ;;
    *)
      AC_MSG_WARN([dynamic linking? don't know how ...])
      ;;
  esac
fi
if test "$dynamic" = true ; then
  AC_MSG_RESULT(yes)
  LIBSUFFIX=$SHLIBSUFFIX
else
  AC_MSG_RESULT(no)
  LIBSUFFIX=.a
fi

AC_SUBST(LIBSUFFIX)
AC_SUBST(LDFLAGS)
AC_SUBST(COMMONVER)
dnl AC_SUBST(CLIENTLIBVER)

dnl required packages:
dnl ems_common
dnl ems-clientlib
dnl

AC_ARG_WITH(ems-libs, [  --with-ems-libs search first in this directory for ems libraries],
	emslibs=$withval, emslibs="")

EMS_SEARCH_COMMON
EMS_SEARCH_COMMONLIB(.$COMMONVER$LIBSUFFIX)
AC_SUBST(EMSCOMMON)
AC_SUBST(EMSEXCOMMON)
AC_SUBST(EMSCOMMONLIB)

dnl EMS_SEARCH_CLIENT
dnl EMS_SEARCH_CLIENTLIB(xx.$CLIENTLIBVER$LIBSUFFIX)
dnl AC_SUBST(EMSCLIENT)
dnl AC_SUBST(EMSCLIENTLIB)

AC_ARG_ENABLE(hbook, [  --enable-hbook compile programs using hbook])
if test "$enable_hbook" = yes ; then
  EXTRASOURCES="$EXTRASOURCES \$(HSOURCES)"
fi
AC_SUBST(EXTRASOURCES)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/cdefs.h)

dnl Checks for ntohl()
AC_MSG_CHECKING([whether ntohl() is properly defined])
AC_CACHE_VAL(ems_cv_header_ntohl_def,
AC_TRY_COMPILE(
  [
  #include <sys/types.h>
  #include <netinet/in.h>
  ] , [
unsigned int res;
res=ntohl(17);
],
ems_cv_header_ntohl_def=yes,
ems_cv_header_ntohl_def=no))
AC_MSG_RESULT($ems_cv_header_ntohl_def)
if test $ems_cv_header_ntohl_def = no ; then
	AC_DEFINE(NEED_NTOHLDEF)
fi

AC_MSG_CHECKING([whether sa_handler accepts int])
AC_CACHE_VAL(ems_cv_header_sa_handler_int,
AC_TRY_COMPILE([#include <signal.h>], [
void (*handler)(int);
struct sigaction vec;
vec.sa_handler=handler;
], ems_cv_header_sa_handler_int=yes, ems_cv_header_sa_handler_int=no))
AC_MSG_RESULT($ems_cv_header_sa_handler_int)
if test $ems_cv_header_sa_handler_int = no ; then
	AC_DEFINE(SA_HANDLER_VOID)
fi

AC_MSG_CHECKING([whether sigpause() is defined])
AC_CACHE_VAL(ems_cv_header_sigpause_def,
AC_EGREP_CPP(sigpause, [#include <sys/signal.h>],
ems_cv_header_sigpause_def=yes,
ems_cv_header_sigpause_def=no))
AC_MSG_RESULT($ems_cv_header_sigpause_def)
if test $ems_cv_header_sigpause_def = no ; then
	AC_DEFINE(NEED_SIGPAUSEDEF)
fi

AC_MSG_CHECKING([whether gettimeofday() is defined])
AC_CACHE_VAL(ems_cv_header_gettimeofday_def,
	AC_EGREP_CPP(gettimeofday, [#include <sys/time.h>],
	ems_cv_header_gettimeofday_def=yes, 
	ems_cv_header_gettimeofday_def=no))
AC_MSG_RESULT($ems_cv_header_gettimeofday_def)
if test $ems_cv_header_gettimeofday_def = no ; then
	AC_DEFINE(NEED_GETTIMEOFDAYDEF)
fi

AC_MSG_CHECKING([whether random() is defined])
AC_CACHE_VAL(ems_cv_header_random_def,
	AC_EGREP_CPP(random, [#include <stdlib.h>],
	ems_cv_header_random_def=yes, 
	ems_cv_header_random_def=no))
AC_MSG_RESULT($ems_cv_header_random_def)
if test $ems_cv_header_random_def = no ; then
	AC_DEFINE(NEED_RANDOMDEF)
fi

AC_MSG_CHECKING([whether ioctl() is defined])
AC_CACHE_VAL(ems_cv_header_ioctl_def,
AC_TRY_COMPILE([#include <sys/ioctl.h>], [
ioctl(0, 0, (char*)0);
], ems_cv_header_ioctl_def=yes, ems_cv_header_ioctl_def=no))
AC_MSG_RESULT($ems_cv_header_ioctl_def)
if test $ems_cv_header_ioctl_def = no ; then
	AC_DEFINE(NEED_IOCTLDEF)
fi

AC_CONFIG_HEADER(config.h)
AC_OUTPUT(Makefile)
